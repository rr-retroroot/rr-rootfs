# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=rr-recovery
pkgver=1
pkgrel=1
pkgdesc="RetroRoot Recovery"
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL')
url='https://github.com/rr-retroroot/rr-recovery'

# every retroroot pkg must be a member of "retroroot" AND "game", "emulator", "app" or "system" group
groups=('retroroot' 'system')

# a retroroot package should not depends on anything as required libraries
# are in "retroroot-sysroot-*arch*" toolchain package. If not present in sysroot,
# the package should build and link a static library here.
# this package is a special case...
#depends=('')

source=("git+$url.git")
sha512sums=('SKIP')

prepare() {
  cd rr-recovery
  git submodule update --init --recursive
}

build() {
  cd rr-recovery

  source /opt/retroroot/rr-env ${CARCH}

  retroroot-cmake \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=${RETROROOT_PREFIX} \
    -DPLATFORM_LINUX=ON -DOPTION_SDL2=ON -DOPTION_RENDER_GLES2=ON

  make $MAKEFLAGS -C build
}

package() {
  cd rr-recovery
  DESTDIR="$pkgdir" make -C build install
}
