# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=rr-libretro-gambatte
pkgver=1062
pkgrel=1
pkgdesc='Nintendo Game Boy/Game Boy Color core'
arch=('x86_64' 'armv7h' 'aarch64' 'riscv64')
url=https://github.com/libretro/gambatte-libretro
license=(GPL2)

# every retroroot pkg must be a member of "retroroot" AND "game", "emulator", "app" or "system" group
groups=('retroroot' 'system' 'cores')

provides=('libretro-gambatte')
conflicts=('libretro-gambatte')

#depends=('libretro-core-info')
#makedepends=('git')

_commit=36a0da43fe6a82aba6acc5336574dbd749b18fa8
source=(libretro-gambatte::git+https://github.com/libretro/gambatte-libretro.git#commit=${_commit})
sha256sums=('SKIP')

pkgver() {
  cd libretro-gambatte
  git rev-list --count HEAD
}

build() {
  source ${RETROROOT_HOME}/toolchain/bin/rr-env
  make -C libretro-gambatte -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-gambatte/gambatte_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

