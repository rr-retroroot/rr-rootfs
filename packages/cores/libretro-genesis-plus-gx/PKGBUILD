# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=rr-libretro-genesis-plus-gx
pkgver=2207
pkgrel=1
pkgdesc='Sega MS/GG/MD/CD core'
arch=('x86_64' 'armv7h' 'aarch64' 'riscv64')
url=https://github.com/libretro/Genesis-Plus-GX
license=(BSD LGPL2.1)

# every retroroot pkg must be a member of "retroroot" AND "game", "emulator", "app" or "system" group
groups=('retroroot' 'system' 'cores')

provides=('libretro-genesis-plus-gx')
conflicts=('libretro-genesis-plus-gx')

depends=('libvorbis' 'zlib')
#makedepends=('git')

_commit=bf492bf3532b9d30e7a023e4329e202b15169e1c
source=(libretro-genesis-plus-gx::git+https://github.com/libretro/Genesis-Plus-GX.git#commit=${_commit})
sha256sums=('SKIP')

pkgver() {
  cd libretro-genesis-plus-gx
  git rev-list --count HEAD
}

build() {
  source ${RETROROOT_HOME}/toolchain/bin/rr-env
  make \
    SHARED_LIBVORBIS=1 \
    SYSTEM_ZLIB=1 \
    -C libretro-genesis-plus-gx \
    -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-genesis-plus-gx/genesis_plus_gx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-genesis-plus-gx/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libretro-genesis-plus-gx/
}

