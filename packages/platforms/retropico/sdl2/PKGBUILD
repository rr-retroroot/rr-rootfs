# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=sdl2-kms-rgb565
pkgver=2.30.11
pkgrel=4
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2)"
arch=('aarch64' )
url="https://www.libsdl.org"
license=('Zlib')

# every retroroot pkg must be a member of "retroroot" AND "game", "emulator", "app" or "system" group
groups=('retroroot' 'system' 'rpi' 'retropico')

depends=('glibc' 'libgl')
makedepends=('libgl' 'alsa-lib')
#depends=('glibc' 'libgl' 'hidapi' 'libusb')
#makedepends=('alsa-lib' 'libgl' 'ibus')
source=("https://github.com/libsdl-org/SDL/releases/download/release-${pkgver}/SDL2-${pkgver}.tar.gz" "kms-add-rgb565.patch")
sha512sums=(
  'cd4c040ebe4ec74250e32b401a292658353721dda30ad1066522b2a9de9a07560313978880a0bad7e7f5103cc14278fcbe27dbc5a188189e7fb6f097e7308550'
  'SKIP'
)

provides=('sdl2')
conflicts=('sdl2')

prepare() {
  cd "SDL2-${pkgver}"
  patch -Np1 < "../kms-add-rgb565.patch"
}

build() {
  cd "SDL2-${pkgver}"

  source ${RETROROOT_HOME}/toolchain/bin/rr-env
  
  CFLAGS+=" -ffat-lto-objects"
  rr-configure ${CARCH} \
    --prefix=/usr \
    --disable-video-opengl \
    --disable-video-opengles1 \
    --disable-video-x11 \
    --disable-pulseaudio \
    --disable-esd \
    --disable-video-wayland \
    --disable-video-rpi \
    --disable-video-vulkan \
    --disable-joystick-virtual \
    --enable-video-kmsdrm \
    --enable-video-opengles2 \
    --enable-alsa

    make $MAKEFLAGS
}

package() {
  cd "SDL2-${pkgver}"

  source ${RETROROOT_HOME}/toolchain/bin/rr-env
  
  make DESTDIR="${pkgdir}" install
}

