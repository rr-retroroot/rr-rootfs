# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=linux-surfacert
pkgver=6.6.22
pkgrel=6
pkgdesc="RetroRoot surfacert kernel"
arch=('armv7h')
license=('GPL')
url="https://gitlab.com/grate-driver/linux/-/archive/v${pkgver}-lts/linux-v${pkgver}-lts.tar.gz"

# every retroroot pkg must be a member of "retroroot" AND "game", "emulator", "app" or "system" group
groups=('retroroot' 'system' 'surfacert')

depends=('coreutils' 'mkinitcpio>=0.7')
makedepends=('rr-environment' 'rr-toolchain-armv7h')
options=(!debug !strip)

source=("$url" "config-surfacert")
sha512sums=('SKIP' 'SKIP')

prepare() {
  _builddir="$srcdir/linux-v${pkgver}-lts"
  cd "$_builddir"
  cp "$srcdir/config-surfacert" .config
  make ARCH="arm" olddefconfig
}

build() {
  cd "$srcdir"/linux-v${pkgver}-lts
  
  source /opt/retroroot/rr-env ${CARCH}
  yes "" | make $MAKEFLAGS ARCH="arm" CROSS_COMPILE=${RETROROOT_CROSS_PREFIX} \
    KBUILD_BUILD_VERSION="$((pkgrel + 0))-retroroot"
}

package() {
  cd "$srcdir"/linux-v${pkgver}-lts
  
  # install kernel image and dtb
  install -d "$pkgdir"/boot
  cp arch/arm/boot/zImage "$pkgdir"/boot/zImage
  cp arch/arm/boot/dts/nvidia/tegra30-microsoft-surface-rt-efi.dtb "$pkgdir"/boot
  
  # install kernel modules
  make ARCH="arm" INSTALL_MOD_PATH="$pkgdir"/usr modules_install
  # remove build and source links
  rm -rf "$pkgdir"/usr/lib/modules/${pkgver}/{source,build}
  
  # TODO: install kernel headers
  #make INSTALL_HDR_PATH="$pkgdir"/usr header_install
  
  # copy surfacert rootfs overlays
  cp -r "$startdir"/overlay/. "${pkgdir}"
}

