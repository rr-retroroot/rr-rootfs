# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=rr-cmake
pkgver=1
pkgrel=1
arch=(any)
pkgdesc="retroroot cmake wrapper"
depends=('rr-environment' 'rr-pkg-config' 'rr-sysroot')
license=("GPL")
url="https://github.com/rr-retroroot/rr-packages"
source=("toolchain.cmake" "retroroot-cmake")
md5sums=('SKIP' 'SKIP')

# every retroroot pkg must be a member of "retroroot" AND "game", "emulator", "app" or "system" group
groups=('retroroot' 'toolchain')

_architectures="x86_64 armv7h aarch64"

build() {
  for _arch in ${_architectures}; do
    sed "s|@TRIPLE@|${_arch}|g" retroroot-cmake > ${_arch}-retroroot-cmake
    sed "s|@TRIPLE@|${_arch}|g" toolchain.cmake > ${_arch}-toolchain.cmake
    sed -i "s|@PROC@|${_arch}|g" ${_arch}-toolchain.cmake
    sed -i "s|@TARGET@|${_arch}-linux-gnu|g" ${_arch}-toolchain.cmake
  done
}

package() {
  _architectures="x86_64 armv7h aarch64"
  for _arch in ${_architectures}; do
    install -d ${pkgdir}/opt/retroroot/target/${_arch}/usr/bin
    install -d ${pkgdir}/opt/retroroot/target/${_arch}/usr/lib/cmake
    install -m755 ${_arch}-retroroot-cmake ${pkgdir}/opt/retroroot/target/${_arch}/usr/bin/retroroot-cmake
    install -m755 ${_arch}-toolchain.cmake ${pkgdir}/opt/retroroot/target/${_arch}/usr/lib/cmake/rr-toolchain.cmake
  done
}

