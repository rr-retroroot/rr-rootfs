# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Themaister <maister@archlinux.us>
# Contributor: lifning <definelightning@gmail.com>

pkgname=retroarch
pkgver=1.18.0
pkgrel=1
pkgdesc='Reference frontend for the libretro API'
arch=('armv7h')
url=http://www.libretro.com/
license=(GPL)
groups=(libretro)
depends=(
  alsa-lib
  dbus
  ffmpeg
  flac
  fontconfig
  fribidi
  gcc-libs
  glibc
  libass.so
  libdrm
  libfreetype.so
  libgl
  libpulse
  libudev.so
  libusb
  libx11
  libxcb
  libxext
  libxinerama
  libxkbcommon
  libxrandr
  libxxf86vm
  libxv
  #mbedtls2
  mesa
  openssl
  #qt5-base
  sdl2
  v4l-utils
  #wayland
  zlib
)
makedepends=(
  #gamemode
  git
  #libdecor
  vulkan-icd-loader
  #wayland-protocols
)
optdepends=(
  #'gamemode: GameMode support'
  #'libdecor: Wayland decorations'
  'libretro-overlays: Collection of overlays'
  'libretro-shaders: Collection of shaders'
  'python: retroarch-cg2glsl'
  'retroarch-assets-ozone: Ozone menu assets'
  'retroarch-assets-xmb: XMB menu assets'
)
backup=(etc/retroarch.cfg)
_tag=06fa5325f8b3cd42e6fba3d57835d5924c9ea2e7
source=(
  git+https://github.com/libretro/RetroArch.git#tag=${_tag}
  retroarch-config.patch
)
md5sums=('SKIP'
         '60030d42fad12e1b391b55aa1c78760a')

pkgver() {
  cd RetroArch
  git describe --tags | sed 's/^v//'
}

prepare() {
  cd RetroArch
  patch -Np1 -i ../retroarch-config.patch
}

build() {
  cd RetroArch
  #export CFLAGS+=' -I/usr/include/mbedtls2'
  #export LDFLAGS+=' -L/usr/lib/mbedtls2'
  #--disable-builtinmbedtls \
  ./configure \
    --prefix=/usr \
    --disable-builtinflac \
    --disable-builtinzlib \
    --disable-cg \
    --disable-jack \
    --disable-oss \
    --disable-sdl \
    --disable-wayland \
    --disable-qt \
    --disable-opengl \
    --enable-opengles \
    --enable-dbus \
    --enable-builtinmbedtls
  make
  make -C libretro-common/audio/dsp_filters
  make -C gfx/video_filters
}

package() {
  cd RetroArch
  make DESTDIR="${pkgdir}" install
  install -Dm 644 libretro-common/audio/dsp_filters/*.{dsp,so} -t "${pkgdir}"/usr/lib/retroarch/filters/audio/
  install -Dm 644 gfx/video_filters/*.{filt,so} -t "${pkgdir}"/usr/lib/retroarch/filters/video/
}

# vim: ts=2 sw=2 et:
